pipeline WorldbankDataProcessingPipeline {

    DataExtractor
        -> XLSXFileInterpreter
        -> SheetPicker
        -> CountryCodeRenamer
        -> HeaderRenamer
        -> DataRangeSelector
        -> BondDataInterpreter
        -> BondDataLoader;
        
    DataRangeSelector
        -> GDPDataInterpreter
        -> GDPDataLoader;

    
    block DataExtractor oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    // Interpreting the XLSX file format
    block XLSXFileInterpreter oftype XLSXInterpreter { }

    // Selecting the relevant sheet "Figure S5.1.2"
    block SheetPicker oftype SheetPicker {
        sheetName: 'Figure S5.1.2';
    }

    // Renaming the "Country Code" column
    block CountryCodeRenamer oftype CellWriter {
        at: range P2:P2;
        write: [
            "Country Code"
        ];
    }

    // Renaming the "GDP per Capita" and "Bond Issuance Share" columns through the instructions
    block HeaderRenamer oftype CellWriter {
        at: range R2:S2;
        write: [
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }

    // Data Ranging
    block DataRangeSelector oftype CellRangeSelector {
        select: range P2:S45;
    }

    // Bond_data interpretation and processing
    block BondDataInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype Alpha3CountryCode,
            "Bond Issuance Share" oftype PercentageRange
        ];
    }
// For bond data loading by following the instructions
    block BondDataLoader oftype SQLiteLoader {
        table: "bondIssuanceData";
        file: "country-stats.sqlite";
    }

    // Gross domestic data interpretation and processing
    block GDPDataInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype Alpha3CountryCode,
            "GDP per Capita" oftype PositiveDecimal
        ];
    }

    block GDPDataLoader oftype SQLiteLoader {
        table: "gdpPerCapitaData";
        file: "country-stats.sqlite";
    }
}
// Validation step
valuetype Alpha3CountryCode oftype text {
    constraints: [
        CountryCodeValidator
    ];
}
constraint CountryCodeValidator oftype RegexConstraint {
    regex: /^[A-Z]{3}$/; 
}

valuetype PositiveDecimal oftype decimal {
    constraints: [
        PositiveValueConstraint
    ];
}
constraint PositiveValueConstraint on decimal: value > 0; 

valuetype PercentageRange oftype decimal {
    constraints: [
        ValidPercentageConstraint
    ];
}
constraint ValidPercentageConstraint on decimal: value >= 0 and value <= 1; 
