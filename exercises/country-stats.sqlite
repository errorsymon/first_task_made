
pipeline World_bank_data {
// Pipline root to node
    WorldbankExtractor
        -> FileXLSXInterpreter
        -> FigureSheetpicker
        -> ChangeCountryCode
        -> ChangeGDPAndBond
        -> DataRange
        -> BondTableInterpreter
        -> BondLoader;


// Ranging data following the instructions
    DataRange
        -> GDPTableInterpreter
        -> GDPLoader;

// World Bank data extracting through the given link
    block WorldbankExtractor oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    block FileXLSXInterpreter oftype XLSXInterpreter { }
// Specification of sheet picking 
    block FigureSheetpicker oftype SheetPicker {
        sheetName: 'Figure S5.1.2';
    }
// Country code changing through cell writing
    block ChangeCountryCode oftype CellWriter {
        at: range P2:P2;
        write: [
            "Country Code",
        ];
    }
// Changing in gdp and bond issunce share value
    block ChangeGDPAndBond oftype CellWriter {
        at: range R2:S2;
        write: [
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }
// Specific data selction for P2: S45
    block DataRange oftype CellRangeSelector {
        select: range P2:S45;
    }
// Table interpreting

    block BondTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype ISO_3166_1,
            "Bond Issuance Share" oftype valueBetweenZeroAndOne
        ];
    }
// Creation of Sqlite file
    block BondLoader oftype SQLiteLoader {

        table: "bondIssuance";
        file: "country-stats.sqlite";
    }

// Defines the format of two column

    block GDPTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype ISO_3166_1,
            "GDP per Capita" oftype positiveDecimal
        ];
    }
// Gdp block for per capita 
    block GDPLoader oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "country-stats.sqlite";
    }

}
// Value type to avoid error 
valuetype ISO_3166_1 oftype text {
    constraints: [
        ISO_3166_1_Validator
    ];
}
// Regexconstraint to a value where it can search for A-Z value and other denominators
constraint ISO_3166_1_Validator oftype RegexConstraint {
    regex: /^[A-Z]{3}$/;
}
// Positive value validations
valuetype positiveDecimal oftype decimal {
    constraints: [
        positiveDecimalValidator
    ];
}
constraint positiveDecimalValidator on decimal: value > 0;
// Define the value betwwen zero and one
valuetype valueBetweenZeroAndOne oftype decimal {
    constraints: [
        valueBetweenZeroAndOneValidator
    ];
}
constraint valueBetweenZeroAndOneValidator on decimal: value >= 0 and value <= 1;
