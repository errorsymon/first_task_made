pipeline GTFS {

    block GTFSDataExtractor oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block GTFSDataInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block GTFSFilePicker oftype FilePicker {
        path: "/stops.txt";
    }

    block GTFSTextFileInterpreter oftype TextFileInterpreter {}

    block GTFSCSVInterpreter oftype CSVInterpreter {
        enclosing: '"';
        enclosingEscape: '"';
    }

    block GTFSTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype BIGINT, 
            "stop_name" oftype GermanText, 
            "stop_lat" oftype Latitude, 
            "stop_lon" oftype Longitude, 
            "zone_id" oftype ZoneID
        ];
    }

    block GTFSDataLoader oftype SQLiteLoader {
        table: "stops";
        file: "gtfs.sqlite";
    }

    // Defining custom value types and constraints

    valuetype GermanText oftype text {
        constraints: [
            GermanUmlautsConstraint
        ];
    }

    constraint GermanUmlautsConstraint on text: value matches /^[A-Za-z0-9äöüÄÖÜß\s\-\.(),]*$/;

    valuetype Latitude oftype decimal {
        constraints: [
            LatitudeRange
        ];
    }

    valuetype Longitude oftype decimal {
        constraints: [
            LongitudeRange
        ];
    }

    constraint LatitudeRange oftype RangeConstraint {
        lowerBound: -90;
        lowerBoundInclusive: true;
        upperBound: 90;
        upperBoundInclusive: true;
    }

    constraint LongitudeRange oftype RangeConstraint {
        lowerBound: -90;
        lowerBoundInclusive: true;
        upperBound: 90;
        upperBoundInclusive: true;
    }

    valuetype ZoneID oftype integer {
        constraints: [
            ZoneRange
        ];
    }

    constraint ZoneRange oftype RangeConstraint {
        lowerBound: 1925;
        upperBound: 1925;
    }

    
    GTFSDataExtractor 
    -> GTFSDataInterpreter 
    -> GTFSFilePicker
    -> GTFSTextFileInterpreter
    -> GTFSCSVInterpreter
    -> GTFSTableInterpreter
    -> GTFSDataLoader;
}
