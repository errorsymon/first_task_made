pipeline TransitDataPipeline {

    // Defining the pipeline flow
    TransitDataDownloader 
    -> TransitDataProcessor 
    -> TransitDataSelector
    -> TransitDataTextInterpreter
    -> TransitDataCSVParser
    -> TransitDataValidator
    -> TransitDataLoader;

    block TransitDataDownloader oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block TransitDataProcessor oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block TransitDataSelector oftype FilePicker {
        path: "/stops.txt";
    }

    block TransitDataTextInterpreter oftype TextFileInterpreter {}

    block TransitDataCSVParser oftype CSVInterpreter {
        enclosing: '"';
        enclosingEscape: '"';
    }

    block TransitDataValidator oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer, 
            "stop_name" oftype GermanUmlauts, 
            "stop_lat" oftype BetweenPlusMinusNinety, 
            "stop_lon" oftype BetweenPlusMinusNinety, 
            "zone_id" oftype ValidZone
        ];
    }

    block TransitDataLoader oftype SQLiteLoader {
        table: "stops";
        file: "gtfs.sqlite";
    }

    // Defining custom value types and constraints

    valuetype GermanUmlauts oftype text {
        constraints: [
            TransitDataGermanUmlautsConstraint
        ];
    }

    constraint TransitDataGermanUmlautsConstraint on text: value matches /^[A-Za-z0-9äöüÄÖÜß\s\-\.(),]*$/;

    valuetype BetweenPlusMinusNinety oftype decimal {
        constraints: [
            TransitDataBetweenPlusMinusNinetyConstraint
        ];
    }

    constraint TransitDataBetweenPlusMinusNinetyConstraint oftype RangeConstraint {
        lowerBound: -90;
        lowerBoundInclusive: true;
        upperBound: 90;
        upperBoundInclusive: true;
    }

    valuetype ValidZone oftype integer {
        constraints: [
            TransitDataValidZoneConstraint
        ];
    }

    constraint TransitDataValidZoneConstraint oftype RangeConstraint {
        lowerBound: 1925;
        upperBound: 1925; // Ensure that only the zone 1925 is selected
    }
}
